# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux AT free DOT fr>
_gemname=backports
pkgname=ruby-$_gemname
pkgver=3.6.1
pkgrel=1
pkgdesc='Essential backports that enable some of the really nice features of Ruby 1.8.7, Ruby 1.9 and Rails from Ruby 1.8.6 and earlier.'
arch=(any)
url='http://rubygems.org/gems/backports'
license=(MIT)
depends=('ruby')
noextract=($_gemname-$pkgver.gem)
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem"
        LICENSE )
sha256sums=('78a1f4b758d8012ed050c4586db94133baa2b9a526059311b4764af8ebff4ea0'
            '1c9395683e9522c0831b6240516d360a78b9675bd86561ea837c2d28619e24a5')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
