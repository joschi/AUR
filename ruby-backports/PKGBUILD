# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux AT free DOT fr>
_gemname=backports
pkgname=ruby-$_gemname
pkgver=3.3.5
pkgrel=1
pkgdesc='Essential backports that enable some of the really nice features of Ruby 1.8.7, Ruby 1.9 and Rails from Ruby 1.8.6 and earlier.'
arch=(any)
url='http://rubygems.org/gems/backports'
license=(MIT)
depends=('ruby')
makedepends=('rubygems')
noextract=($_gemname-$pkgver.gem)
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem"
        LICENSE )

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
md5sums=('3d213a3433b792dee132ecdfbc0a882c'
         '46d4f8c6df6ebd990ec06c371ceaf731')
