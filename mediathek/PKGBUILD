# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Soeren Koerner <nonick at posteo dot de>
# Contributor: Benedikt 'linopolus' Mueller <benemue at googlemail dot com>

pkgname=mediathek
pkgver=6
pkgrel=1
pkgdesc="Offers access to the Mediathek of different tv stations (ARD, ZDF, Arte, etc.)"
arch=(any)
url="http://zdfmediathk.sourceforge.net"
license=('custom')
depends=('java-runtime')
optdepends=('mplayer: record streams'
            'flvstreamer: stream flash'
            'vlc: play files directly from mediathek')
options=(!strip !zipman)
source=(http://downloads.sourceforge.net/zdfmediathk/MediathekView_${pkgver}.zip
        $pkgname
        $pkgname.desktop)
sha256sums=('422dcbc021d5b227b4882dafbdfa97bd60e4816e1c6111a0cebe4a069fc13659'
            '3367f3076966910f53f21d9fbb904afdd887c560ddef2e53e2d9a28ae08c542f'
            'c98a6e86990dc8d4054f1975e836fc470ad6115042327154b93f06d39d588ded')

package() {
  install -d $pkgdir/{opt/$pkgname/{lib,bin},usr/{bin,share/{{doc,licenses}/$pkgname,applications,pixmaps}}}
  install -m755 $pkgname $pkgdir/usr/bin/
  install -m644 MediathekView.jar $pkgdir/opt/$pkgname/
  install -m644 -t $pkgdir/opt/$pkgname/lib lib/*
  install -m755 bin/flv.sh $pkgdir/opt/$pkgname/bin/
  install -m644 $pkgname.desktop $pkgdir/usr/share/applications/
  install -m644 info/MediathekView.png $pkgdir/usr/share/pixmaps/
  install -m644 Anleitung/Kurzanleitung.pdf $pkgdir/usr/share/doc/$pkgname/
  install -m644 -t $pkgdir/usr/share/licenses/$pkgname Copyright/{*.*,_copyright}
}
