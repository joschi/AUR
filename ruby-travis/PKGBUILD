# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=travis
pkgname=ruby-$_gemname
pkgver=1.2.0
pkgrel=1
pkgdesc='CLI and Ruby client library for Travis CI'
arch=(any)
url='https://github.com/travis-ci/travis'
license=('MIT')
depends=(
  'ruby' 'ruby-backports>=2.6' 'ruby-faraday>=0.8' 'ruby-faraday_middleware>=0.9'
  'ruby-netrc>=0.7' 'ruby-highline>=1.6' 'ruby-gh>=0.9' 'ruby-launchy>=2.1'
  'ruby-pry>=0.9' 'ruby-net-http-persistent>=2.8')
makedepends=('rubygems')
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
md5sums=('7995af92e217e48968030a8e74fd68a7')
