# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=travis
pkgname=ruby-$_gemname
pkgver=1.5.8
pkgrel=1
pkgdesc='CLI and Ruby client library for Travis CI'
arch=(any)
url='https://github.com/travis-ci/travis'
license=('MIT')
depends=(
  'ruby'
  'ruby-faraday>=0.8.7'
  'ruby-faraday_middleware>=0.9'
  'ruby-highline>=1.6'
  'ruby-netrc>=0.7'
  'ruby-backports'
  'ruby-gh>=0.13.0'
  'ruby-launchy>=2.1'
  'ruby-pry>=0.9'
  'ruby-typhoeus>=0.6'
  'ruby-pusher-client>=0.4'
  'ruby-addressable>=2.3')
makedepends=('rubygems')
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('44dc1193607cfb2649c5b50d1befdf68')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
