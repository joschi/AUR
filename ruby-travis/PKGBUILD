# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=travis
pkgname=ruby-$_gemname
pkgver=1.6.17
pkgrel=1
pkgdesc='Travis CI Client (CLI and Ruby library)'
arch=(any)
url='https://github.com/travis-ci/travis.rb'
license=('MIT')
depends=(
  'ruby'
  'ruby-faraday>=0.9'
  'ruby-faraday_middleware>=0.9.1'
  'ruby-highline>=1.6'
  'ruby-backports'
  'ruby-gh>=0.13.0'
  'ruby-launchy>=2.1'
  'ruby-pry-0.9'
  'ruby-typhoeus>=0.6.8'
  'ruby-pusher-client>=0.4'
  'ruby-addressable>=2.3')
noextract=($_gemname-$pkgver.gem)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
sha256sums=('81b11bc61e8b815d3c29f67d07ad16c72f783cce665537acfc72bfd67db20878')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
