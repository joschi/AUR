# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
_gemname=pry
pkgname=ruby-$_gemname-0.9
pkgver=0.9.12.6
pkgrel=2
pkgdesc='An IRB alternative and runtime developer console'
arch=('any')
url='http://pry.github.com'
license=('MIT')
depends=('ruby'
  'ruby-coderay>=1.0'
  'ruby-slop>=3.4'
  'ruby-method_source>=0.8')
options=(!emptydirs)
noextract=($_gemname-$pkgver.gem)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
sha256sums=('0107730b35bdd4fd042fa9c92263174a38f975844230bc5c583fd4816c55af72')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  mv "$pkgdir/usr/bin/pry" "$pkgdir/usr/bin/pry-0.9"
}

# vim:set ts=2 sw=2 et:
