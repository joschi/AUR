# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
_gemname=pry
pkgname=ruby-$_gemname-0.9
pkgver=0.9.12.6
pkgrel=1
pkgdesc='An IRB alternative and runtime developer console'
arch=('any')
url='http://pry.github.com'
license=('MIT')
depends=('ruby'
  'ruby-coderay>=1.0'
  'ruby-slop>=3.4'
  'ruby-method_source>=0.8')
options=(!emptydirs)
noextract=($_gemname-$pkgver.gem)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
sha256sums=('0107730b35bdd4fd042fa9c92263174a38f975844230bc5c583fd4816c55af72')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  local _bindir="$(ruby -e'puts Gem.bindir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  mv "$pkgdir/$_bindir/pry" "$pkgdir/$_bindir/pry-0.9"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
