# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
pkgname=ruby-lint
pkgver=0.9.1
pkgrel=1
pkgdesc='A linter and static code analysis tool for Ruby.'
arch=(any)
url='https://github.com/yorickpeterse/ruby-lint/'
license=('MIT')
depends=(
	'ruby'
	'ruby-parser>=2.0.0'
	'ruby-racc>=1.4.10'
	'ruby-slop')
makedepends=('rubygems')
noextract=($pkgname-$pkgver.gem)
source=(https://rubygems.org/downloads/$pkgname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$pkgname-$pkgver.gem"
}
md5sums=('e20ef347565ad6196139bc5c6e2d82c0')
