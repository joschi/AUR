# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
pkgname=ruby-lint
pkgver=1.1.0
pkgrel=1
pkgdesc='A linter and static code analysis tool for Ruby.'
arch=(any)
url='https://github.com/yorickpeterse/ruby-lint/'
license=('MIT')
depends=(
	'ruby'
	'ruby-parser>=2.1.1'
	'ruby-slop>=3.4.7')
noextract=($pkgname-$pkgver.gem)
source=(https://rubygems.org/downloads/$pkgname-$pkgver.gem)
sha256sums=('c9186839359a306998a4833cb4d1e92bee491b809c591d04a70380f43d679e4d')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$pkgname-$pkgver.gem"
}
