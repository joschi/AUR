# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Niels Abspoel <aboe76@gmail.com>
_gemname=hiera
pkgname=ruby-$_gemname
pkgver=1.3.2
pkgrel=2
pkgdesc="A pluggable data store for hierarcical data."
arch=(any)
url='http://projects.puppetlabs.com/projects/hiera/'
license=('MIT')
depends=('ruby' 'ruby-json_pure')
source=(
        "https://rubygems.org/downloads/$_gemname-$pkgver.gem"
        'hiera.yaml')
noextract=($_gemname-$pkgver.gem)
backup=('etc/hiera.yaml')

package() {
  cd "$srcdir"

  install -d $pkgdir/usr/bin

  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  # Create hiera default directory
  install -d $pkgdir/var/lib/hiera

  # Create hiera default configuration
  install -d $pkgdir/etc
  install -m 644 $srcdir/hiera.yaml $pkgdir/etc/
}
sha256sums=('425a4c50d68780a83a82ea8d82ac39b47bbca2ee40bed48f37829d56a8c314db'
            '814a19b0c7f17a77cfc3449516e1decd0e017c37ed7301aef350193c32d24799')
