# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Konstantin Nikiforov <helllamer@gmail.com> 
# Contributor: Alper Kanat <alperkanat@raptiye.org>
# Contributor: adam2fours <adam@2fours.com>

# check() function is used to verify GPG signature. check() imports 3 keys into your GPG keyring at first build.
# See http://scarybeastsecurity.blogspot.com/2011/07/alert-vsftpd-download-backdoored.html for reason of this step.
# If you have problems with gpg, you can remove check() function, and all will be ok.

_pkgname=cassandra
pkgname="${_pkgname}-2.0"
pkgver=2.0.10
pkgrel=1
pkgdesc='Apache Cassandra 2.0 NoSQL database'
arch=('any')
url='http://cassandra.apache.org/'
license=('APACHE')
depends=('java-runtime')
makedepends=('gnupg')
checkdepends=('wget')
optdepends=('python: to use Python CLI administration scripts')
provides=('cassandra')
backup=(etc/cassandra/cassandra-env.sh
        etc/cassandra/cassandra-rackdc.properties
        etc/cassandra/cassandra-topology.properties
        etc/cassandra/cassandra.yaml
        etc/cassandra/commitlog_archiving.properties
        etc/cassandra/log4j-server.properties
        etc/cassandra/log4j-tools.properties)
install=cassandra.install
_url_tgz="http://www.apache.org/dist/${_pkgname}/${pkgver}/apache-${_pkgname}-${pkgver}-bin.tar.gz"
source=("${_url_tgz}"
        '01_fix_cassandra_home_path.patch'
        'cassandra.install'
        'cassandra.service'
        'cassandra-tmpfile.conf')
sha256sums=('962ab35b1767ed4c56eb9a2a6b9df374097dc48d4f1c718945cab3a977e5d9fa'
            'bbb5dcc19cac4e19c506210da901280c3063a6a241480bf12bc874e6a5c02657'
            '4ba9f4d2b68cf1d142471aec145ffd656e6af6473352fe2e41552f55f39eb09e'
            '1940bf0b68abff7f5e389df61fcdd0f12c03394c1ebdce9a6e3658993b620ff8'
            'c786b224827b313255912ea42a1e312d22d28b9b9e8dcb1a091c18eaa6081acf')

build() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  patch -p0 < ${srcdir}/01_fix_cassandra_home_path.patch
}

## to check gpg signature
check() {
  msg "Checking GPG signature..."
  msg2 "(To disable gpg-check: build with '--nocheck')"
  gpg --list-keys | grep 'sylvain@datastax.com' || {
    _url_keys='https://www.apache.org/dist/cassandra/KEYS'
    msg "No maintainers' GPG keys found. Importing from ${_url_keys} ..."
    wget --quiet -O - ${_url_keys} | gpg --import -
  }
  # no need to add signature to package dependences
  echo "${_url_tgz}.asc"
  wget --quiet -O - "${_url_tgz}.asc" | gpg --verify - "apache-${_pkgname}-${pkgver}-bin.tar.gz"
  msg2 "Detached GPG signature is valid."
}

package() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  mkdir -p ${pkgdir}/{etc/cassandra,var/log/cassandra}
  mkdir -p ${pkgdir}/{usr/bin,usr/share/cassandra,usr/share/java/cassandra}

  cp -a interface pylib tools ${pkgdir}/usr/share/cassandra/

  mkdir -p ${pkgdir}/usr/share/cassandra/bin/
  for f in bin/*; do
    if [[ ! "${f}" == *.bat && -x ${f} ]]; then
      cp -a ${f} ${pkgdir}/usr/share/cassandra/bin/
      ln -s /usr/share/cassandra/${f} ${pkgdir}/usr/${f}
    fi
  done
  cp -a bin/cassandra.in.sh ${pkgdir}/usr/share/cassandra/

  cp -a lib/* ${pkgdir}/usr/share/java/cassandra/
  ln -s ../java/cassandra ${pkgdir}/usr/share/cassandra/lib

  cp -a conf/* ${pkgdir}/etc/cassandra/
  ln -s /etc/cassandra ${pkgdir}/usr/share/cassandra/conf

  install -Dm644 ${srcdir}/cassandra.service ${pkgdir}/lib/systemd/system/cassandra.service
  install -Dm644 ${srcdir}/cassandra-tmpfile.conf ${pkgdir}/usr/lib/tmpfiles.d/cassandra.conf
}

# vim:set ts=2 sw=2 et:
