# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Guillaume ALAUX <guillaume at alaux dot net>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Konstantin Nikiforov <helllamer@gmail.com> 
# Contributor: Alper Kanat <alperkanat@raptiye.org>
# Contributor: adam2fours <adam@2fours.com>
_pkgname=cassandra
pkgname="${_pkgname}-1.2"
pkgver=1.2.16
pkgrel=1
pkgdesc='NoSQL database (precompiled version)'
arch=('any')
url='http://cassandra.apache.org/'
license=('APACHE')
depends=('java-runtime')
optdepends=('python: to use Python CLI administration scripts')
provides=('cassandra')
backup=(etc/cassandra/cassandra-env.sh
        etc/cassandra/cassandra-rackdc.properties
        etc/cassandra/cassandra-topology.properties
        etc/cassandra/cassandra.yaml
        etc/cassandra/commitlog_archiving.properties
        etc/cassandra/log4j-server.properties
        etc/cassandra/log4j-tools.properties)
_url_tgz="http://mirror.netcologne.de/apache.org/${_pkgname}/${pkgver}/apache-${_pkgname}-${pkgver}-bin.tar.gz"
install="${_pkgname}.install"
source=("${_url_tgz}"
        "${_pkgname}.service"
        "${_pkgname}.install"
        '01_fix_cassandra_home_path.patch')

build() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  for p in 01_fix_cassandra_home_path
  do
    patch -p0 < ${srcdir}/${p}.patch
  done
}

package() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  mkdir -p ${pkgdir}/{usr/bin,etc/cassandra}
  mkdir -p ${pkgdir}/{usr/bin,usr/share/cassandra,usr/share/java/cassandra}

  cp -a interface pylib tools ${pkgdir}/usr/share/cassandra/

  mkdir -p ${pkgdir}/usr/share/cassandra/bin/
  for f in bin/*; do
    if [[ ! "${f}" == *.bat && -x ${f} ]]; then
      cp -a ${f} ${pkgdir}/usr/share/cassandra/bin/
      ln -s /usr/share/cassandra/${f} ${pkgdir}/usr/${f}
    fi
  done
  cp -a bin/cassandra.in.sh ${pkgdir}/usr/share/cassandra/

  cp -a lib/* ${pkgdir}/usr/share/java/cassandra/
  ln -s ../java/cassandra ${pkgdir}/usr/share/cassandra/lib

  cp -a conf/* ${pkgdir}/etc/cassandra/
  ln -s /etc/cassandra ${pkgdir}/usr/share/cassandra/conf

  install -Dm644 ${srcdir}/cassandra.service \
                 ${pkgdir}/lib/systemd/system/cassandra.service
}
sha256sums=('5ff7a844fca5ec326729001703aaca436543e560485ac50a1c9a5185c205ec46'
            '01efb0f346e90e9df2eac3063abeb0125f20b4d7ab7464d5f43c25d34e450149'
            'c9744196d5e78928c065ae388659078ac7d34583cd29d62c98240fa9748832f2'
            'bbb5dcc19cac4e19c506210da901280c3063a6a241480bf12bc874e6a5c02657')
