# Maintainer: Guillaume ALAUX <guillaume at alaux dot net>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Konstantin Nikiforov <helllamer@gmail.com> 
# Contributor: Alper Kanat <alperkanat@raptiye.org>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

pkgname=cassandra
pkgver=2.0.3
pkgrel=1
pkgdesc='NoSQL database (precompiled version)'
arch=('any')
url='http://cassandra.apache.org/'
license=('APACHE')
depends=('java-runtime')
optdepends=('python: to use Python CLI administration scripts')
backup=(etc/cassandra/cassandra-env.sh
        etc/cassandra/cassandra-rackdc.properties
        etc/cassandra/cassandra-topology.properties
        etc/cassandra/cassandra.yaml
        etc/cassandra/commitlog_archiving.properties
        etc/cassandra/log4j-server.properties
        etc/cassandra/log4j-tools.properties)
install="${pkgname}.install"
source=("http://mirror.netcologne.de/apache.org/${pkgname}/${pkgver}/apache-${pkgname}-${pkgver}-bin.tar.gz"
        '01_fix_cassandra_home_path.patch'
        "${pkgname}.service.systemd"
        "${pkgname}.install")

build() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  for p in 01_fix_cassandra_home_path
  do
    patch -p0 < ${srcdir}/${p}.patch
  done
}

package() {
  cd ${srcdir}/apache-cassandra-${pkgver}

  mkdir -p ${pkgdir}/etc/cassandra
  mkdir -p ${pkgdir}/usr/{bin,share/cassandra,share/java/cassandra}

  cp -a interface pylib tools ${pkgdir}/usr/share/cassandra/

  mkdir -p ${pkgdir}/usr/share/cassandra/bin/
  for f in bin/*; do
    if [[ ! "${f}" == *.bat && -x ${f} ]]; then
      cp -a ${f} ${pkgdir}/usr/share/cassandra/bin/
      ln -s /usr/share/cassandra/${f} ${pkgdir}/usr/${f}
    fi
  done
  cp -a bin/cassandra.in.sh ${pkgdir}/usr/share/cassandra/

  cp -a lib/* ${pkgdir}/usr/share/java/cassandra/
  ln -s ../java/cassandra ${pkgdir}/usr/share/cassandra/lib

  cp -a conf/* ${pkgdir}/etc/cassandra/
  ln -s /etc/cassandra ${pkgdir}/usr/share/cassandra/conf

  install -Dm644 ${srcdir}/cassandra.service.systemd \
                 ${pkgdir}/lib/systemd/system/cassandra.service
}
md5sums=('98d266fa0b84b50971e87f0c905bf2df'
         '8d6129b0bf4e5d8a2cf8efea3d1279bd'
         '3188c59c5355c591e001fc4d18c5bb6e'
         '638f2f0fd09293b67e5b3fe5c8e1a243')
