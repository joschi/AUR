# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=wasabi
pkgname=ruby-$_gemname
pkgver=3.1.0
pkgrel=1
pkgdesc='A simple WSDL parser for Ruby'
arch=(any)
url='https://github.com/savonrb/wasabi'
license=('MIT')
depends=('ruby' 'ruby-httpi>=2.0' 'ruby-nokogiri>=1.4.0')
makedepends=('rubygems')
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
md5sums=('8b118cb164bf8a465f6c68b5606124ab')
