# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=fpm-cookery
pkgname=ruby-$_gemname
pkgver=0.18.0
pkgrel=1
pkgdesc='A tool for building software packages with fpm.'
arch=(any)
url='https://github.com/bernd/fpm-cookery'
license=('MIT')
depends=(
  'ruby'
  'ruby-fpm'
  'ruby-facter'
  'puppet'
  'ruby-addressable'
  'ruby-systemu')
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha256sums=('8574c5e438f058b9144a13db04961d1ac19f7e35106f79d99dcd245bf91fc848')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
