# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
pkgname=datastax-devcenter
pkgver=1.2.1.201411071159
pkgrel=1
pkgdesc='Visual query tool for Apache Cassandra and DataStax Enterprise clusters'
arch=('i686' 'x86_64')
url='http://www.datastax.com/what-we-offer/products-services/devcenter'
license=('Commercial')
depends=('java-environment')

_arch='x86_64'
if [[ $CARCH = 'i686' ]]; then
  _arch='x86'
fi

source=("http://downloads.datastax.com/devcenter/DevCenter-${pkgver}-linux-gtk-${_arch}.tar.gz"
        'datastax-devcenter.desktop'
        'datastax-devcenter.sh')
sha256sums=('dcf14892c4e52051f935e5cc0c9e4308ee47e605f6a958e614a3564cbac6868d'
            '54db4f64e8ba8b9619dda6c629e4d2cff0d8419ede546f0acd66d5e97ac651a2'
            'c048cc7f8c82bb528550a486669536e3a8cdde270519db66f72e545e98760f3e')
[[ "$CARCH" = 'i686' ]] && sha256sums[0]='283952c84416e23a11de42e11cbb2b4b14d9a9c2a29393f7fdf893812c1dc5be'

package() {
  mkdir -p ${pkgdir}/usr/share/
  mv "${srcdir}/DevCenter" "${pkgdir}/usr/share/datastax-devcenter"

  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
