# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
# Contributor: helios <aur@wiresphere.de>
pkgname=vagrant
pkgver=1.2.1
pkgrel=1
pkgdesc="Tool for building and distributing virtualized development environments"
arch=('i686' 'x86_64')
url='http://vagrantup.com/'
license=('MIT')
depends=('ruby' 'virtualbox>=4.0' 'ruby-net-ssh>=2.6.6' \
         'ruby-net-scp>=1.1.0' 'ruby-erubis>=2.7.0' 'ruby-i18n>=0.6.0' \
         'ruby-log4r>=1.1.9' 'ruby-childprocess>=0.3.7')
makedepends=('git' 'rubygems' 'rake' 'ruby-bundler')
options=(!strip)
source=("https://github.com/mitchellh/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('bafaf972296d0e8a122bc2ca6e13091f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Patch gemspec file
  sed -i -e 's/\(\"rspec-[a-z]*\", \"~>\) 2.11.0/\1 2.13.0/' "${pkgname}.gemspec"

  rake build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}"/usr/bin "pkg/${pkgname}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 et:
