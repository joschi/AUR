# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Konstantin Nikiforov <helllamer@gmail.com> 
# Contributor: adam2fours <adam@2fours.com>
# Thanks to Alper Kanat <alperkanat@raptiye.org> for cassandra rc.d script. 
pkgname=dsc-cassandra
pkgver=2.0.7
pkgrel=1
pkgdesc='DataStax Community Edition distribution of Apache Cassandra'
arch=('any')
url='http://www.datastax.com/'
license=('Apache')
makedepends=('gnupg')
depends=('java-runtime' 'python2')
provides=('cassandra')
source=("http://downloads.datastax.com/community/${pkgname}-${pkgver}-bin.tar.gz"
	'cassandra.conf'
	'cassandra.service'
	"${pkgname}.install"
        '01_fix_cassandra_home_path.patch')
install="${pkgname}.install"
backup=(
	'etc/cassandra/cassandra-env.sh'
	'etc/cassandra/cassandra-topology.properties'
	'etc/cassandra/cassandra.yaml'
	'etc/cassandra/commitlog_archiving.properties'
	'etc/cassandra/log4j-server.properties'
	'etc/cassandra/log4j-tools.properties')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	for p in 01_fix_cassandra_home_path;
	do
		patch -p0 < ${srcdir}/${p}.patch
	done
}

package() {
	cd $srcdir/${pkgname}-${pkgver}

	mkdir -p ${pkgdir}/etc
	mkdir -p ${pkgdir}/usr/bin
	mkdir -p ${pkgdir}/usr/share/cassandra
	mkdir -p ${pkgdir}/usr/share/java/cassandra
	mkdir -p ${pkgdir}/usr/lib/python2.7/site-packages

	cp -a pylib/cqlshlib ${pkgdir}/usr/lib/python2.7/site-packages
	cp -a {lib,interface} ${pkgdir}/usr/share/cassandra/
	cp -a conf ${pkgdir}/etc/cassandra/
	ln -s /etc/cassandra ${pkgdir}/usr/share/cassandra/conf

	cd "${srcdir}/${pkgname}-${pkgver}/bin"

	install \
		cassandra \
		cassandra-cli \
		cqlsh \
		json2sstable \
		nodetool \
		sstable2json \
		sstablekeys \
		sstableloader \
		sstablescrub \
		stop-server \
		${pkgdir}/usr/bin/

	install \
		cassandra.in.sh \
		${pkgdir}/usr/share/cassandra/

	install -D -m755 ${startdir}/cassandra.conf ${pkgdir}/etc/conf.d/cassandra
	install -D -m644 ${srcdir}/cassandra.service \
                 ${pkgdir}/lib/systemd/system/cassandra.service
}
sha256sums=('671ae08ce04a7c6117acc644646fabcb26b8062c4dd73f6e2dc570450f0cdc78'
            'a28cfeab35106bc6ae67c90ea994d6f6b0810a7e82d2e5799e30e7cceff28577'
            '46267af7d4ee40e4b9182d2c155874ea7dc258692d32ebf370a27f42c6f86fff'
            '7e0565e73079e4c38ef1f620377a56fc39aaa42a5c1a98b50b86c230ef47e5fa'
            'bbb5dcc19cac4e19c506210da901280c3063a6a241480bf12bc874e6a5c02657')
