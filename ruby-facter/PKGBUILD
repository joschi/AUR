# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=facter
pkgname=ruby-$_gemname
pkgver=1.7.1
pkgrel=1
pkgdesc="A library for collecting facts about your system."
arch=(any)
url="http://puppetlabs.com/projects/facter/"
license=('MIT')
depends=('ruby')
makedepends=('rubygems')
optdepends=('net-tools: for ifconfig-based facts')
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
conflicts=('facter')
provides=('facter')
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
md5sums=('2903da75c8b9f45a733550d098a868d1')
