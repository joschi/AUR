# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>

# check() function is used to verify GPG signature. check() imports 3 keys into your GPG keyring at first build.
# See http://scarybeastsecurity.blogspot.com/2011/07/alert-vsftpd-download-backdoored.html for reason of this step.
# If you have problems with gpg, you can remove check() function, and all will be ok.

_pkgname=cassandra
pkgname=$_pkgname-devel
_pkgver=2.1.0
pkgver=${_pkgver}_rc4
pkgrel=1
pkgdesc='Apache Cassandra NoSQL database (development version)'
arch=('any')
url='http://cassandra.apache.org/'
license=('APACHE')
conflicts=('cassandra' 'cassandra-1.2' 'dsc-cassandra' 'dsc-cassandra-1.2')
depends=('java-runtime')
makedepends=('gnupg')
checkdepends=('wget')
optdepends=('python: to use Python CLI administration scripts')
backup=(etc/cassandra/cassandra-env.sh
        etc/cassandra/cassandra-rackdc.properties
        etc/cassandra/cassandra-topology.properties
        etc/cassandra/cassandra-topology.yaml
        etc/cassandra/cassandra.yaml
        etc/cassandra/commitlog_archiving.properties
        etc/cassandra/logback.xml
        etc/cassandra/logback-tools.xml)
install=cassandra.install
_url_tgz="http://archive.apache.org/dist/${_pkgname}/${_pkgver}/apache-${_pkgname}-${pkgver/_/-}-bin.tar.gz"
source=(${_url_tgz}
        01_fix_cassandra_home_path.patch
        cassandra.install
        systemd_cassandra.service)
sha256sums=('cdd4c6c967083ce1547db4dce9bb16af0fa0ed06b5c58bf27fab002e954845c4'
            'bbb5dcc19cac4e19c506210da901280c3063a6a241480bf12bc874e6a5c02657'
            'e74b3c6568ad7858966de7671334d8a1539e43b81737b8a12011dbd84526b9f2'
            '8cc75f282436fbd0ee533b175749c04e19e137a26131b22b1f39a9ee5869e60e')

build() {
  cd ${srcdir}/apache-cassandra-${pkgver/_/-}

  patch -p0 < ${srcdir}/01_fix_cassandra_home_path.patch
}

## to check gpg signature
check() {
  msg "Checking GPG signature..."
  msg2 "(To disable gpg-check: build with '--nocheck')"
  gpg --list-keys | grep 'sylvain@datastax.com' || {
    _url_keys='https://www.apache.org/dist/cassandra/KEYS'
    msg "No maintainers' GPG keys found. Importing from ${_url_keys} ..."
    wget --quiet -O - ${_url_keys} | gpg --import -
  }
  # no need to add signature to package dependences
  echo "${_url_tgz}.asc"
  wget --quiet -O - "${_url_tgz}.asc" | gpg --verify - "apache-${_pkgname}-${pkgver/_/-}-bin.tar.gz"
  msg2 "Detached GPG signature is valid."
}

package() {
  cd ${srcdir}/apache-cassandra-${pkgver/_/-}

  mkdir -p ${pkgdir}/{usr/bin,etc/cassandra,var/log/cassandra,var/log/cassandra}
  mkdir -p ${pkgdir}/{usr/bin,usr/share/cassandra,usr/share/java/cassandra}

  cp -a interface pylib tools ${pkgdir}/usr/share/cassandra/

  mkdir -p ${pkgdir}/usr/share/cassandra/bin/
  for f in bin/*; do
    if [[ ! "${f}" == *.bat && -x ${f} ]]; then
      cp -a ${f} ${pkgdir}/usr/share/cassandra/bin/
      ln -s /usr/share/cassandra/${f} ${pkgdir}/usr/${f}
    fi
  done
  cp -a bin/cassandra.in.sh ${pkgdir}/usr/share/cassandra/

  cp -a lib/* ${pkgdir}/usr/share/java/cassandra/
  ln -s ../java/cassandra ${pkgdir}/usr/share/cassandra/lib

  cp -a conf/* ${pkgdir}/etc/cassandra/
  ln -s /etc/cassandra ${pkgdir}/usr/share/cassandra/conf

  install -Dm644 ${srcdir}/systemd_cassandra.service \
                 ${pkgdir}/lib/systemd/system/cassandra.service

  chown http:log ${pkgdir}/var/log/cassandra
}
