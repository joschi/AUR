# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=puppet
pkgname=ruby-$_gemname
pkgver=3.1.0
pkgrel=2
pkgdesc='A system for automating system administration tasks.'
arch=(any)
url='http://puppetlabs.com/projects/puppet/'
license=('Apache')
backup=("etc/puppet/puppet.conf")
options=(emptydirs)
depends=('ruby' 'ruby-hiera-puppet' 'ruby-shadow')
optdepends=('ruby-facter')
makedepends=('rubygems')
conflicts=('puppet' 'puppet3')
provides=('puppet')
install='puppet.install'
source=(
        "https://rubygems.org/downloads/${_gemname}-${pkgver}.gem"
        "fileserver.conf"
        "puppet"
        "puppet.confd"
        "puppet.conf"
        "puppetagent.service"
        "puppetmaster"
        "puppetmaster.confd"
        "puppetmaster.service")
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    --no-ri --no-rdoc -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  # Configuration and rc scripts
  install -d $pkgdir/etc/{puppet,rc.d}
  install -m 644 $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf
  install -m 644 $srcdir/fileserver.conf $pkgdir/etc/puppet/fileserver.conf

  # Setup tmpfiles.d config
  install -d $pkgdir/etc/tmpfiles.d
  echo "D /var/run/puppet 0755 puppet puppet -" > $pkgdir/etc/tmpfiles.d/puppet.conf

  # Configuration for hiera / symlink is there to use hiera within puppet.
  ln -snf /etc/hiera.yaml ${pkgdir}/etc/puppet/hiera.yaml

  # Systemd files
  install -Dm 644 $srcdir/puppetagent.service $pkgdir/usr/lib/systemd/system/puppetagent.service
  install -Dm 644 $srcdir/puppetmaster.service $pkgdir/usr/lib/systemd/system/puppetmaster.service

  # Create puppet homedir
  install -d $pkgdir/var/run/puppet
  install -d $pkgdir/var/lib/puppet

  # Init scripts
  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster
  install -D $srcdir/puppet.confd $pkgdir/etc/conf.d/puppet
  install -D $srcdir/puppetmaster.confd $pkgdir/etc/conf.d/puppetmaster

  # Compress and install man pages
  install -d $pkgdir/usr/share/man
  mv $pkgdir/usr/lib/ruby/gems/1.9.1/gems/$_gemname-$pkgver/man/* $pkgdir/usr/share/man
  gzip -9 -q -r $pkgdir/usr/share/man
}
md5sums=('1e9fc8e751e9c3728411c7ba46030071'
         'ffacd9bfb9c7656a3a345dbc5eb0e67a'
         '7248a78029191360fd7b042a8d5fa381'
         '84c4895abb7fc6281b553aef315560af'
         'a7d61d20e46b411eb50ad5279987584a'
         'bbfe0b847f08012cd534d96fe3758b3e'
         '78a1279055cadcb7e62fb4379db9637f'
         '4fbb2e7ed1a9eb36f2d79cce3824d916'
         '15037c609ec10a6046387c43d1641464')
