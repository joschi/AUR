# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
pkgname=jq
pkgver=1.1
pkgrel=1
pkgdesc='Lightweight and flexible command-line JSON processor'
arch=('i686' 'x86_64')
url="http://stedolan.github.com/jq/"
license=('MIT')
groups=()
depends=()
makedepends=('flex' 'bison' 'python' 'make' 'gcc')
checkdepends=('valgrind')
options=()
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/stedolan/${pkgname}/tarball/${pkgname}-${pkgver}")
noextract=("${pkgname}-${pkgver}.tar.gz")
md5sums=('c8a1369ec979ed39a5bfed87368005c5')

build() {
  cd "$srcdir"
  test -d "$pkgname-$pkgver" || mkdir "$pkgname-$pkgver"
  tar --strip-components=1 -C "$pkgname-$pkgver" -xzf ${pkgname}-${pkgver}.tar.gz

  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d -m 0755 "${pkgdir}/usr/bin"
  install -m 0755 jq "${pkgdir}/usr/bin"
}

# vim:set ts=2 sw=2 et:
