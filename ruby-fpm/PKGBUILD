# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=fpm
pkgname=ruby-$_gemname
pkgver=1.0.0
pkgrel=1
pkgdesc="Convert directories, RPMs, Python eggs, Rubygems, and more to RPMs, DEBs, Solaris packages and more."
arch=(any)
url="https://github.com/jordansissel/fpm"
license=('MIT')
depends=('ruby' 'ruby-backports>=2.6.2' 'ruby-json' 'ruby-arr-pm>=0.0.8' 'ruby-clamp>=0.6.0'
         'ruby-cabin>=0.6.0' 'ruby-ftw' 'ruby-childprocess' 'ruby-ffi')
makedepends=('rubygems')
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
md5sums=('15f3310484af511c3d955fb1cf03ea48')
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
}
