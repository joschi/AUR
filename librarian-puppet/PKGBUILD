# Maintainer: Jochen Schalanda <jochen+aur AT schalanda.aname>
pkgname=librarian-puppet
pkgver=1.0.3
_gemname='librarian-puppet'
pkgrel=1
pkgdesc='A bundler for your Puppet infrastructure.'
arch=(any)
url='http://librarian-puppet.com/'
license=('MIT')
depends=('ruby' 'ruby-librarian>=0.1.2')
noextract=(${_gemname}-${pkgver}.gem)
source=(http://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
sha256sums=('2c7a3d3350f3eee63f6d6eea469e1f9eb4d3eb5acb44a680aa042f35107e5dbd')

package() {
  cd "${srcdir}"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  # Remove vendored gems
  rm -rf "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/vendor/gems"
  rm -rf "${pkgdir}${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 et:
